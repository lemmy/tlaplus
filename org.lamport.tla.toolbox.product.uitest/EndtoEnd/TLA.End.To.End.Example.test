--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: TLA.End.To.End.Example
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _0133MAldEem_kewt0eDFQIQ
Runtime-Version: 2.4.1.201903190000
Save-Time: 8/27/19, 12:10 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

try -command {
	// Create new spec from build-in example.
	get-view "Welcome View" | get-button "Missionaries and Cannibals(TLA+)" | click
	get-window -class WizardDialog | get-button Finish | click
	get-view "Spec Explorer" | get-tree | select "MissionariesAndCannibals \\[ MissionariesAndCannibals.tla \\]/model" +
		"s/Model_1" | double-click

	// Run model checking.
	get-editor Model_1 | get-button "Runs TLC on the model." | click

	// wait for model checking to finish.
	wait-run
	
//	set-dialog-result File "/tmp/tlaplus/tlaplus-master/runtime-org.lamport.tla.toolbox.product.product.product/TExpression.tla"
	set-dialog-result File [concat [get-workspace-location] "/MissionariesAndCannibals/TExpression.tla"]
	get-menu -path "File/Open Module/Add TLA+ Module..." | click
	get-window "TLA+ Module is not found" | get-button Yes | click
	with [get-editor TExpression | get-text-viewer] {
		set-caret-pos 2 1
		type-text "EXTENDS Toolbox"
		key-type Enter -times 1
		type-text "tExpression == _TETrace[_TEPosition]"
		key-type "M1+s"
	}
	get-menu -path "File/Parse Module" | click

	// validate the error trace.
	get-view "TLC Errors" | get-section "Error-Trace" | get-button "Toggle between expand and collapse all (Shift+Cli" +
		"ck to restore the default two-level expansion)" | click
	get-view "TLC Errors" | get-section "Error-Trace" | get-tree | get-property itemCount | equals 12 | verify-true

	// Expand the Error-Trace Exploration section if collapsed.
	if [get-view "TLC Errors" | get-section "Error-Trace Exploration" | get-property "isExpanded()" -raw | not] {
		get-view "TLC Errors" | get-section "Error-Trace Exploration" | click
	}

	// 
	get-view "TLC Errors" | get-section "Error-Trace Exploration" | get-button "Extend extra modules in trace express" +
		"ions which are not extended by the actual spec." | click
	with [get-window "Extra Modules"] {
		with [get-table] {
			select Randomization | get-item -path Randomization | check
			select TExpression | get-item -path TExpression | check
		}
		get-button OK | click
	}
	
	// Insert new trace explorer expression.
	get-view "TLC Errors" | get-section "Error-Trace Exploration" | get-button Add | click
	try -command {
		with [get-window -class WizardDialog] {
			with [get-text-viewer] {
				type-text "selfRef == RandomSubset(1, {1})"
			}
			get-button Finish | click
		}
	} -catch {
		// This probably means that the section is collapsed instead of expanded.
		log -message "Error" -severity error -plugin "org.lamport.tla.toolbox.product.uitests"
			echo "Failed to open the Error-Trace Exploration expression input dialog" | log -severity warning 
	}	

	// // Insert new trace explorer expression.
	get-view "TLC Errors" | get-section "Error-Trace Exploration" | get-button Add | click
	try -command {
		with [get-window -class WizardDialog] {
			with [get-text-viewer] {
				type-text tExpression
			}
			get-button Finish | click
		}
	} -catch {
		// This probably means that the section is collapsed instead of expanded.
		log -message Error -severity error -plugin "org.lamport.tla.toolbox.product.uitests"
		echo "Failed to open the Error-Trace Exploration expression input dialog" | log -severity warning
	}

	// Run trace exploration.
	get-view "TLC Errors" | get-section "Error-Trace Exploration" | get-button Explore | click

	wait-te

    // Export trace to clipboard before the validation below sets a selection
	with [get-view "TLC Errors" | get-section "Error-Trace"] {
	    get-button "Click to export error-trace to clipboard assequence of records. Shift-click to omit the action's position (_TEPosition), name, and location." 
	        | click
	}

	// Validate error trace exploration
	get-view "TLC Errors" | get-section "Error-Trace" | get-tree | get-cell 5 1 | click
	get-view "TLC Errors" | get-section "Error-Trace" | get-text-viewer | get-property text | contains "[bank_of_boat |->" | verify-true
	
	// Paste exported trace into ECE and verify it is valid TLA+
	with [get-editor "Model_1"] {
	    click
	    with [get-section "Evaluate Constant Expression"] {
	        with [get-text-viewer] {
	            key-type "M1+v"
	            type-text "[2]._TEAction.name"
	        }
	        get-button "No Behavior Spec" | check
	    }
	    get-button "Runs TLC on the model." | click
	    with [get-section "Evaluate Constant Expression"] {
	        get-text-viewer "Value:" | get-property text | contains "Next" | verify-true
	    }
	}
} -finally {
	DeleteSpecNew
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
